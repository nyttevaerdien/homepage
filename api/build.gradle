plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.cloud.tools.jib' version '3.4.0'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'dev.kotprotiv'
version = '2.3.0'

java {
	sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'com.h2database:h2'

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'junit:junit'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

jib {
	from {
		image = 'eclipse-temurin:21-jdk-alpine'
	}
	to {
		image = 'nyttevaerdien/homepage-api' + ':' + rootProject.version
	}
	container {
		creationTime = 'USE_CURRENT_TIMESTAMP'
	}
}

spotless {
	java {
		googleJavaFormat('1.22.0')
	}
}
